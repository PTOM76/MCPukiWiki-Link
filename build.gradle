plugins {
    id 'fabric-loom' version '1.10-SNAPSHOT'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.+'
    id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "pkwklink" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

repositories {
    mavenLocal()
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases" }
    maven { url "https://maven.nucleoid.xyz/" }
    maven { url "https://maven.pitan76.net/" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "net.pitan76:mcpitanlib-fabric${project.mcpitanlib_version}"
    include "net.pitan76:mcpitanlib_checker-fabric:${project.mcpitanlib_checker_version}"

    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    implementation "net.pitan76:pukibot:1.0.0"
    include "net.pitan76:pukibot:1.0.0"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

apply plugin: "com.modrinth.minotaur"
apply plugin: "com.matthewprenger.cursegradle"

if (System.getenv("CURSEFORGE_TOKEN") != null) {
    curseforge {
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        project {
            id = '528338'
            changelog = project.changelog + "\nMCPitanLib version: " + mcpitanlib_version.split(":")[1]
            releaseType = 'release'
            addGameVersion "Fabric"
            addGameVersion '1.18'
            addGameVersion '1.18.1'
            addGameVersion '1.18.2'
            addGameVersion '1.19'
            addGameVersion '1.19.1'
            addGameVersion '1.19.2'
            addGameVersion '1.20'
            addGameVersion '1.20.1'
            addGameVersion '1.20.3'
            addGameVersion '1.20.4'
            addGameVersion '1.21'
            addGameVersion '1.21.1'
            addGameVersion '1.21.2'
            addGameVersion '1.21.3'
            relations {
                requiredDependency "fabric-api"
                requiredDependency "mcpitanlibarch"
                optionalDependency "techreborn"
                optionalDependency "modmenu"
                optionalDependency "storagebox-fabric"
                optionalDependency "cloth-config"
            }

            mainArtifact(tasks.remapJar)
        }
    }
}

if (System.getenv("MODRINTH_TOKEN") != null) {
    modrinth {
        token = System.getenv("MODRINTH_TOKEN")
        projectId = 'JcIgEOzn'
        versionNumber = project.mod_version + "-fabric"
        gameVersions = ['1.18', '1.18.1', '1.18.2', '1.19', '1.19.1', '1.19.2', '1.20', '1.20.1', '1.20.3', '1.20.4', '1.21', '1.21.1', '1.21.2', '1.21.3']
        versionType = 'release'
        uploadFile = tasks.remapJar
        changelog = project.changelog + "\nMCPitanLib version: " + mcpitanlib_version.split(":")[1]
        loaders = ["fabric"]
        dependencies {
            required.project "P7dR8mSH" // Fabric API
            required.project "uNRoUnGT"
        }
    }
}
